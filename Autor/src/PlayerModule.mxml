<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute"
	width="800" height="512" creationComplete="init()" horizontalAlign="center" currentState="1">
	
	<mx:states>
		<mx:State name="1"/>
		<mx:State name="2">
			<mx:SetProperty target="{slideSpacer}" name="height" value="140"/>
			<mx:SetStyle target="{titleTextInput}" name="fontSize" value="32"/>
			<mx:SetStyle target="{bodyTextArea}" name="fontSize" value="28"/>
		</mx:State>
		<mx:State name="3">
			<mx:RemoveChild target="{slideSpacer}"/>
			<mx:RemoveChild target="{titleCanvas}"/>
			<mx:RemoveChild target="{bodyTextArea}"/>
			<mx:AddChild relativeTo="{slideVBox}" position="lastChild">
				<mx:Canvas id="canvas1" width="100%" height="100%" verticalScrollPolicy="off" horizontalScrollPolicy="off">
					<mx:SWFLoader id="loadingSwf1" source="@Embed('br/com/optimedia/assets/img/loading.swf')"
						scaleContent="false" width="50" height="50" x="350" y="187"/>
					<mx:Image id="slideImage" source="http://www.educar.tv/amf/services/autor/presentationfiles/{presentationID}/{slideVO.text_body}"
						maxWidth="{slideVBox.width}" maxHeight="{slideVBox.width}"
						/>
				</mx:Canvas>
			</mx:AddChild>
			<mx:SetStyle target="{slideVBox}" name="verticalAlign" value="middle"/>
		</mx:State>
	</mx:states>
	
	<mx:Script>
		<![CDATA[
			import mx.core.IChildList;
			import mx.managers.PopUpManagerChildList;
			import mx.rpc.soap.LoadEvent;
			import mx.effects.easing.Elastic;
			import mx.effects.Fade;
			import mx.events.ListEvent;
			import br.com.optimedia.player.view.components.SlideIndexList;
			import mx.controls.videoClasses.VideoPlayer;
			import mx.controls.VideoDisplay;
			import mx.core.UIComponent;
			import mx.events.ResizeEvent;
			import mx.core.IFlexDisplayObject;
			import mx.controls.SWFLoader;
			import mx.managers.PopUpManager;
			import br.com.optimedia.player.view.components.MediaPopUp;
			import fl.video.FLVPlayback;
			import mx.events.CloseEvent;
			import mx.containers.TitleWindow;
			import br.com.optimedia.assets.vo.MediaVO;
			import mx.collections.ArrayCollection;
			import br.com.optimedia.assets.vo.SlideVO;
			import br.com.optimedia.assets.ImgAssets;
			import mx.controls.Alert;
			import br.com.optimedia.player.PlayerFacade;
			
			public static const SET_SLIDE_EVENT:String = "SET_SLIDE_EVENT";
			
			public static const PLAYER_MODE:String = "PLAYER_MODE";
			public static const PREVIEW_MODE:String = "PREVIEW_MODE";
			
			private var playerFacade:PlayerFacade = PlayerFacade.getInstance();
			
			[Bindable]
			public var presentationID:int;
			
			[Bindable]
			public var slideID:Number;
			
			[Bindable]
			[Inspectable(category="General", enumeration="PLAYER_MODE,PREVIEW_MODE", defaultValue="PLAYER_MODE")]
			public var mode:String;
			
			[Bindable]
			public var slideVO:SlideVO;
			
			[Bindable]
			public var slidesArray:ArrayCollection = new ArrayCollection();
			
			private function init():void {
				var titleBackground:Sprite = new Sprite ();
				titleBackground.graphics.lineStyle(1,0x000000);
				titleBackground.graphics.beginFill(0xFFFFFF,0.5);
				titleBackground.graphics.drawRoundRect(20,0,titleCanvas.width*0.95,titleCanvas.height ,20,20);
				titleBackground.graphics.endFill();
				titleCanvas.rawChildren.addChildAt(titleBackground, 0);
				if(mode == PREVIEW_MODE) {
					playerFacade.startup( this );
					slideID = 0;
				}
				
				prevBtn.addEventListener(MouseEvent.CLICK, removeAllPopUps);
				indexBtn.addEventListener(MouseEvent.CLICK, removeAllPopUps);
				nextBtn.addEventListener(MouseEvent.CLICK, removeAllPopUps);
			}
			
			public function start(presentationID:int, slideID:int, mode:String = PLAYER_MODE ):void {
				
				this.presentationID = presentationID;
				this.slideID = slideID;
				this.mode = mode;
				
				if( mode != PLAYER_MODE && mode != PREVIEW_MODE ) {
					Alert.show( mode+" não é um modo válido.", "Erro");
				}
				else {
					playerFacade.startup( this );
				}
			}
			
			public function setSlide(slideVO:SlideVO):void {
				slideID = slideVO.slide_id;
				
				//gambiarra pra evitar slides antigos q foram salvos com tipo 0
				if( slideVO.type_slide_id == 0 ) slideVO.type_slide_id = 1;
				//fim da gambiarra
				
				this.currentState = slideVO.type_slide_id.toString();
				
				this.slideVO = slideVO;
				
				if(mode == PREVIEW_MODE) {
					indexBtn.label = 'Slide '+int(slideVO.page_order)+'/'+slidesArray.length.toString();
				}
				else if(mode == PLAYER_MODE) {
					/* indexBtn.label = 'Slide '+int(slideVO.page_order+1)+'/'+slidesArray.length.toString(); */
					indexBtn.label = 'Slide '+int(getSlideIndex(slideVO.slide_id)+1)+'/'+slidesArray.length.toString();
				}
				
				disableButtons( getSlideIndex( slideVO.slide_id ) );
				createMediaBtns();
				dispatchEvent( new Event(SET_SLIDE_EVENT) );
			}
			
			private function nextBtnClick():void {
				var actualSlideIndex:int = getSlideIndex(slideID);
				var nextSlideIndex:int = actualSlideIndex+1;
				setSlide( slidesArray[nextSlideIndex] );
			}
			
			private function prevBtnClick():void {
				var actualSlideIndex:int = getSlideIndex(slideID);
				var nextSlideIndex:int = actualSlideIndex-1;
				setSlide( slidesArray[nextSlideIndex] );
			}
			
			private function getSlideIndex(slideID:Number):Number {
				for each( var slide:SlideVO in slidesArray ) {
					if( slide.slide_id == slideID ) {
						return slidesArray.getItemIndex( slide );
					}
				}
				return 0;
			}
			
			private function disableButtons(slideIndex:int):void {
				prevBtn.enabled = true;
				nextBtn.enabled = true;
				if( slideIndex == 0 ) {
					prevBtn.enabled = false;
				}
				if( slideIndex == slidesArray.length-1 ) {
					nextBtn.enabled = false;
				}
			}
			
			private function createMediaBtns():void {
				mediaBox.visible = false;
				mediaBox.removeAllChildren();
				for each( var media:MediaVO in slideVO.mediaArray ) {
					var btn:Button = new Button();
					btn.width = 24;
		 			btn.height = 24;
		 			btn.data = media;
					btn.addEventListener(MouseEvent.CLICK, openMediaPopUp);
		 			switch (media.category_id)
					{
						case 1:
							btn.setStyle("icon", ImgAssets.category1Icon);
							btn.toolTip = "Ver Tabela";
							break;
						case 2:
							btn.setStyle("icon", ImgAssets.category2Icon);
							btn.toolTip = "Ver Gráfico";
							break;
						case 3:
							btn.setStyle("icon", ImgAssets.category3Icon);
							btn.toolTip = "Ver Imagem";
							break;
						case 4:
							btn.setStyle("icon", ImgAssets.category4Icon);
							btn.toolTip = "Ver Vídeo";
							break;
						case 5:
							btn.setStyle("icon", ImgAssets.category5Icon);
							btn.toolTip = "Abrir URL";
							break;
						case 6:
							btn.setStyle("icon", ImgAssets.category6Icon);
							btn.toolTip = "Ver Texto";
							break;
						case 7:
							btn.setStyle("icon", ImgAssets.category7Icon);
							btn.toolTip = "Baixar Arquivo";
							break;
						default:
							btn.setStyle("icon", ImgAssets.category3Icon);
							btn.toolTip = "Abrir Mídia";
							break;
					}
					mediaBox.addChild( btn );
				}
				mediaBox.visible = true;
			}
			
			private function removeAllPopUps(e:Event):void {
                popUp.dismissEffect.play();
			}
			
			private var popUp:MediaPopUp = new MediaPopUp();
			private function openMediaPopUp(event:Event):void {
				removeAllPopUps(null);
				
				popUp = new MediaPopUp();
				popUp.maxHeight = slideVBox.height*0.96;
				popUp.maxWidth = slideVBox.width*0.96;
				
				var media:MediaVO;
				if(event is MouseEvent) {
					//SE O EVENTO VEIO DO BOTÃO
					media = event.target.data as MediaVO;
				}
				else if(event is TextEvent) {
					//SE O EVENTO VEIO DO LINK NO TEXTO
					for each( var item:MediaVO in slideVO.mediaArray ) {
						if( item.media_id.toString() == TextEvent(event).text.toString() ){
							media = item;
						}
					}
				}
				
				if( media == null ) {
					Alert.show("Esta mídia não existe mais.", "Atenção");
					return;
				}
				switch(media.category_id) {
					case 4:
						//vídeo
						var movie:FLVPlayback = new FLVPlayback();
						var aux:UIComponent = new UIComponent();
						aux.addChild( movie as DisplayObject );
						movie.addEventListener(LoadEvent.LOAD, function(e:LoadEvent):void { popUp.addChild( aux ); });
						movie.play('http://www.educar.tv/amf/services/autor/mediafiles/'+media.body);
						movie.autoRewind = false;
						movie.skin="http://www.educar.tv/sinase.moodle/interactive/br/com/optimedia/assets/skinOverPlaySeekMute.swf";
						movie.scaleMode = "maintainAspectRatio";
						movie.skinBackgroundColor = 0x666666;
	        			movie.skinAutoHide = true;
						
						popUp.addChild( aux );
						popUp.addEventListener(CloseEvent.CLOSE, function(e:CloseEvent):void { movie.stop(); movie.loaderInfo.loader.close(); })
						PopUpManager.addPopUp( popUp, this, false, PopUpManagerChildList.POPUP );
						PopUpManager.centerPopUp( popUp );
						aux.width = movie.width;
						aux.height = movie.height;
						break;
					case 5:
						//link
						var url:URLRequest = new URLRequest( media.body );
						navigateToURL(url,"_blank");
						break;
					case 6:
						//texto
						var text:TextArea = new TextArea();
						text.setStyle("backgroundAlpha", 0);
						text.setStyle("borderSides",0);
						text.setStyle("fontSize", 22);
						text.htmlText = "<span class='media'>" + media.body + "</span>";
						text.wordWrap = true;
						text.editable = false;
						text.enabled = true;
						popUp.addChild( text );
						PopUpManager.addPopUp( popUp, this );
						PopUpManager.centerPopUp( popUp );
						text.width = 500;
						text.height = 400;
						break;
					case 7:
						//link para slide de outra apresentação
						var url1:URLRequest = new URLRequest( 'http://www.educar.tv/amf/services/autor/mediafiles/'+media.body);
						navigateToURL(url1,"_blank");
						break;
					default:
						//imagens
						var arq:Array = media.body.split(".");
						if (arq[1]=="swf") {
							var swfLoad:SWFLoader = new SWFLoader();
							swfLoad.source = 'http://www.educar.tv/amf/services/autor/mediafiles/'+media.body;
							swfLoad.autoLoad = true;
							swfLoad.scaleContent = true;
							popUp.addChild( swfLoad );
						}else {
							var imagem:Image = new Image();
							imagem.autoLoad = true;
 							imagem.maxWidth = this.width*0.95;
							imagem.maxHeight = this.height*0.95;
							imagem.scaleContent = false;
							imagem.setStyle("horizontalCenter",0);
							imagem.setStyle("verticalCenter",0);
							imagem.source = 'http://www.educar.tv/amf/services/autor/mediafiles/'+media.body;
							popUp.addChild( imagem );
						}
						PopUpManager.addPopUp( popUp, this );
						PopUpManager.centerPopUp( popUp );
						break;
				}
			}
			
			private function indexBtnClick():void {
				var obj:Object = this.getChildByName( SlideIndexList.NAME );
				if( obj ) {
					removeSlideIndexList( null );
				}
				else {
					var index:SlideIndexList = new SlideIndexList();
					index.slideArray = slidesArray;
					index.visible = false;
					this.addChild( index );
					index.listCanvas.x = indexBtn.x + 10;
					index.listCanvas.y = bottomBar.y + 6;
					index.list.selectedItem = slideVO;
					index.list.addEventListener(ListEvent.ITEM_CLICK, function(e:ListEvent):void { setSlide(index.list.selectedItem as SlideVO); index.removeMe(); } );
					bottomImg.addEventListener(MouseEvent.ROLL_OVER, removeSlideIndexList);
					index.visible = true;
				}
			}
			
			private function removeSlideIndexList(e:Event):void {
				var obj:SlideIndexList = this.getChildByName( SlideIndexList.NAME ) as SlideIndexList;
				if( obj && !obj.showEffect.isPlaying && !obj.hideEffect.isPlaying ) {
					SlideIndexList(obj).removeMe();
					bottomImg.removeEventListener(MouseEvent.ROLL_OVER, removeSlideIndexList);
				}
				System.gc();
			}
			
		]]>
	</mx:Script>
	
	<mx:Fade id="mediaEffect" easingFunction="{Elastic.easeOut}" duration="1000"/>
	
	<mx:VBox id="slideVBox" width="100%" height="475" horizontalAlign="center" verticalScrollPolicy="off" horizontalScrollPolicy="off">
		
		<mx:Spacer id="slideSpacer" height="0"/>
		
		<mx:Canvas id="titleCanvas" width="100%" height="50">
			<mx:TextInput id="titleTextInput" verticalCenter="0" horizontalCenter="0" width="714"
				backgroundAlpha="0" borderThickness="0"	borderStyle="none" fontSize="28" text="{slideVO.title}"
				editable="false" textAlign="center"/>
		</mx:Canvas>
		
		<mx:TextArea id="bodyTextArea" width="760" height="100%" fontSize="22" htmlText="{slideVO.text_body}"
			link="openMediaPopUp(event)" editable="false" backgroundAlpha="0" borderStyle="none"/>
		
	</mx:VBox>
	
	<mx:Canvas id="bottomBar" width="100%" bottom="0">
	
		<mx:Image id="bottomImg" source="{ImgAssets.playerTaskbar}" width="784" bottom="0" horizontalCenter="0"/>
		
		<mx:HBox width="784" height="30" bottom="0" verticalAlign="middle" paddingLeft="2" paddingRight="2" horizontalCenter="0">
			
			<mx:Button id="prevBtn" toolTip="Slide Anterior" icon="{ImgAssets.backArrow}" click="prevBtnClick()" enabled="false"/>
			<mx:Button id="indexBtn" toolTip="Lista de slides" width="110" click="indexBtnClick()"/>
			<mx:Button id="nextBtn" toolTip="Próximo slide" icon="{ImgAssets.nextArrow}" click="nextBtnClick()" enabled="false"/>
			
			<mx:Spacer width="100" />
			
			<mx:HBox id="mediaBox" showEffect="{mediaEffect}"/>
				
			<mx:Spacer width="100%" />
			
			<mx:Button id="urlBtn" icon="@Embed(source='br/com/optimedia/assets/img/chain.png')" toolTip="Clique para ver a url deste slide" width="30"/>
			
		</mx:HBox>

	</mx:Canvas>
	
</mx:Module>
