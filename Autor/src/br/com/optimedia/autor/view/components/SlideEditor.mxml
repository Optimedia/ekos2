<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
	xmlns:components="br.com.optimedia.autor.view.components.*" creationComplete="init()">
	
	<mx:Script>
		<![CDATA[
			import mx.rpc.events.ResultEvent;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			import mx.collections.ArrayCollection;
		
			import br.com.optimedia.autor.assets.vo.SlideVO;
			import br.com.optimedia.autor.assets.vo.PresentationVO;
			import br.com.optimedia.autor.AutorFacade;
			
			public static const NEW_SLIDE_EVENT:String = "NEW_SLIDE_EVENT";
			public static const SET_SLIDE_ORDER_EVENT:String = "SET_SLIDE_ORDER_EVENT";
			
			private var autorFacade:AutorFacade = AutorFacade.getInstance();
			private var setOrderPopUp:SetOrderPopUp;
			
			[Bindable]
			public var presentationVO:PresentationVO = new PresentationVO();
			
			private function init():void {
				autorFacade.startup( this );
				
				slideSelector.addEventListener("addSlide", addSlide);
				slideSelector.addEventListener("editSlide", editSlide);
				slideSelector.addEventListener("deleteSlide", deleteSlide);
				slideSelector.addEventListener("setOrder", showSetOrderPopUp);
				repositoryPanel.addEventListener("currentMediaChange", currentMediaChange);
			}
			
			/**
			 * Função para editar um slide
			 * 
			 */ 
			private function editSlide(e:Event):void {
				viewStack.selectedIndex = 1;
				
				textEditor.addEventListener("closeTextEditor", closeTextEditor);
				
				textEditor.slideVO = slideSelector.slideVO;
				textEditor.slideNum = slideSelector.slideNum;
			}
			
			/**
			 * Função que passa para o textEditor qual a média selecionada no momento.
			 * 
			 */ 
			private function currentMediaChange(e:Event):void {
				textEditor.currentMedia = repositoryPanel.currentMedia;
			}
			
			/**
			 * Função que retorna para o SlideSelector.
			 *
			 */  
			private function closeTextEditor(e:Event):void {
				viewStack.selectedIndex = 0;
				textEditor.slideVO = new SlideVO();
			}
			
			/**
			 * Função para chamar o popup
			 * 
			 */ 
			private function showSetOrderPopUp(e:Event):void {				
				setOrderPopUp = new SetOrderPopUp();
				
				setOrderPopUp.slideArray = presentationVO.slidesArray as ArrayCollection;
				
				setOrderPopUp.addEventListener("orderChange", orderChangeHandler);
				
				PopUpManager.addPopUp(setOrderPopUp, this, true);
			}
			
			/**
			 * Função para adicionar um slide
			 * 
			 */ 
			private function addSlide(e:Event):void {
				var slide:SlideVO = new SlideVO();
				slide.presentation_id = presentationVO.presentation_id;
				slide.page_order = uint(slideSelector.slideNum)+ 1;
				
				presentationVO.slidesArray.addItemAt(slide, int(slideSelector.slideNum ));
				
				slideSelector.presentationVO = presentationVO;
				
				this.dispatchEvent( new ResultEvent(NEW_SLIDE_EVENT, false, true, slide) );
			}
			
			/**
			 * Função para deletar um slide
			 * 
			 */ 
			private function deleteSlide(e:Event):void {
				presentationVO.slidesArray.removeItemAt(int(slideSelector.slideNum) - 1);
				
				slideSelector.presentationVO = presentationVO;
				
				// Remover slide do banco
				
				orderChangeHandler();
			}
			
			/**
			 * Função que reorganiza a ordem dos slides vindo do 'addSlide' 'deleteSlide' e 'setOrder'
			 * 
			 */ 
			public function orderChangeHandler(e:Event = null):void {
				
				var tempSlideArray:ArrayCollection = new ArrayCollection();
				
				if(e == null) {
					tempSlideArray = presentationVO.slidesArray;
				} else {
					tempSlideArray = setOrderPopUp.slideArray as ArrayCollection;
				}
				
				var i:int = 1;
				
				var finalSlideArray:Array = new Array();
				
				for each (var slide:SlideVO in tempSlideArray) {
				    slide.page_order = i;
				    finalSlideArray.push(slide);
				    
				    i++;
				}
				
				presentationVO.slidesArray = finalSlideArray;
				
				slideSelector.presentationVO = presentationVO;
				
				this.dispatchEvent( new ResultEvent(SET_SLIDE_ORDER_EVENT, false, true, finalSlideArray) );
				
				if(e != null) {
					setOrderPopUp.closePop();
				}
			}
			
		]]>
	</mx:Script>
	
	<mx:Button id="backBtn" label="Voltar" />
	
	<mx:HBox width="100%" height="100%">
		
		<mx:ViewStack id="viewStack" width="100%" height="100%">
		
			<components:SlideSelector id="slideSelector" presentationVO="{presentationVO}"/>
			
			<components:TextEditor id="textEditor"/>
			
		</mx:ViewStack>
		
		<components:RepositoryPanel id="repositoryPanel" />
		
	</mx:HBox>
	
</mx:VBox>
